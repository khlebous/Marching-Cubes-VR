#pragma kernel CSMain

int _BrushMode;
int _BrushShape;
float4 _BrushColor;
float3 _BrushScale;

float4x4 _FromMcToBrushMatrix;

RWStructuredBuffer<float> _Voxels;
RWStructuredBuffer<float4> _VoxelColors;

float3 _Scale;
uint _Width, _Height, _Depth;


void Draw(int idx, float distanceToBrush) {
	if (_BrushMode == 2)
		_Voxels[idx] = min(_Voxels[idx], distanceToBrush);

	if (_BrushMode == 1)
		_Voxels[idx] = max(_Voxels[idx], 1 - distanceToBrush);
}

float GetDistToBrush(float3 pos) {

	if (_BrushShape == 1) {
		//sphere
		return max(0, distance(pos, 0) - 0.5);
	}
	else {
		//cuboid
		float3 closestPointInCuboid = float3(clamp(pos, -0.5, 0.5));
		return distance(pos, closestPointInCuboid);
	}
}

void ChangeColor(uint3 id)
{
	int idx = id.x + id.y * _Width + id.z * _Width * _Height;
	float4 rawPosition = float4(id.x * _Scale.x, id.y * _Scale.y, id.z * _Scale.z, 1);
	float4 position = mul(_FromMcToBrushMatrix, rawPosition);
	float dist = GetDistToBrush(position.xyz);

	if (dist <= 0)
		_VoxelColors[idx] = _BrushColor;
}

void ChangeShape(uint3 id)
{
	if (id.x == 0 || id.y == 0 || id.z == 0 || id.x == _Width - 1 || id.y == _Height - 1 || id.z == _Depth - 1)
		return;

	int idx = id.x + id.y * _Width + id.z * _Width * _Height;
	float4 rawPosition = float4(id.x * _Scale.x, id.y * _Scale.y, id.z * _Scale.z, 1);
	float4 position = mul(_FromMcToBrushMatrix, rawPosition);
	float dist = GetDistToBrush(position.xyz);

	if (dist <= 1)
		Draw(idx, dist);
}

[numthreads(8, 8, 8)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
	if (_BrushMode == 0)
		return;

	if (_BrushMode == 3)
		ChangeColor(id);
	else
		ChangeShape(id);
}

