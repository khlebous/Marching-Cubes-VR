#pragma kernel CSMain

int _BrushMode;
int _BrushShape;
float4 _BrushColor;
float3 _BrushScale;

float4x4 _FromMcToBrushMatrix;

RWStructuredBuffer<float> _Voxels;
RWStructuredBuffer<float4> _VoxelColors;

float3 _Scale;
uint _Width, _Height, _Depth;


void Draw(int idx, float distanceToBrush) {
	if (_BrushMode == 2)
		_Voxels[idx] = min(_Voxels[idx], distanceToBrush);

	if (_BrushMode == 1)
		_Voxels[idx] = max(_Voxels[idx], 1 - distanceToBrush);
}

float GetDistToBrush(float3 pos) {

	float3 distVector = float3(0, 0, 0);

	if (_BrushShape == 1) {
		//sphere
		if (length(pos) > 0.5) {
			float3 offset = (pos / length(pos)) * (length(pos) - 0.5);
			distVector = float3(offset.x * _BrushScale.x, offset.y * _BrushScale.y, offset.z * _BrushScale.z);
		}
	}
	else {
		//cuboid
		float3 unscaledPos = float3(pos.x * _BrushScale.x, pos.y * _BrushScale.y, pos.z * _BrushScale.z);
		float3 closestPointInCuboid = float3(clamp(unscaledPos, -0.5 * _BrushScale, 0.5* _BrushScale));
		distVector = unscaledPos - closestPointInCuboid;
	}

	return length(distVector);
}

void ChangeColor(uint3 id)
{
	int idx = id.x + id.y * _Width + id.z * _Width * _Height;
	float4 rawPosition = float4(id, 1);
	float4 position = mul(_FromMcToBrushMatrix, rawPosition);
	float dist = GetDistToBrush(position.xyz);

	if (dist <= 0.1) // 0 but is float
		_VoxelColors[idx] = _BrushColor;
}

void ChangeShape(uint3 id)
{
	if (id.x == 0 || id.y == 0 || id.z == 0 || id.x == _Width - 1 || id.y == _Height - 1 || id.z == _Depth - 1)
		return;

	int idx = id.x + id.y * _Width + id.z * _Width * _Height;
	float4 rawPosition = float4(id, 1);
	float4 position = mul(_FromMcToBrushMatrix, rawPosition);
	float dist = GetDistToBrush(position.xyz);

	if (dist <= 1)
		Draw(idx, dist);
}

[numthreads(8, 8, 8)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
	if (_BrushMode == 0)
		return;

	if (_BrushMode == 3)
		ChangeColor(id);
	else
		ChangeShape(id);
}

